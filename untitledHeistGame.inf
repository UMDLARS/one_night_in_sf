Constant Story "The Office";
Constant Headline "^^You step through the front doors of the office of Blamazon. It is pitch black outside. You are here after hours.^^This is a work of ~interactive fiction~.  If this is your first time playing such a game, please type INTRO at the prompt below.^^";
Constant INITIAL_LOCATION_VALUE = Lobby;
Constant OPTIONAL_SIMPLE_DOORS;
Constant OPTIONAL_EXTENDED_VERBSET;
Constant OPTIONAL_PROVIDE_UNDO;
Constant DEBUG;

Constant voxfail = "The tower silently listens to your voice, but then flashes red and buzzes disapprovingly.";
Constant handfail = "You place your hand on the handprint recognizer. Light scans your hand before flashing red and buzzing disapprovingly.";

Include "globals.h";
Include "ext_cheap_scenery.h";
Include "puny.h";
Include "help.h";

attribute heavy;

[UseSub;
	"You're not sure how you can use that in this location.";
];

Verb 'use'
	* noun -> Use
	* noun 'on' noun -> Use
	* noun 'with' noun -> Use;

Verb 'talk'
	* 'to' noun -> TalkTo;

[TalkToSub;
	"Perhaps you should try to ~talk to something (or someone)~.";
];

[JimmySub;
	"That won't work.";
];

Verb 'jimmy'
	* noun 'with' noun -> Jimmy;

[Initialise;
  "It is your first day on the job at Blamazon. You applied here to gain access to their offices after hours to steal the boss's valuable information off of their computer. They have state of the art cyber security preventing any digital leaks of the information so you are here to physically take it.";
];

!===================LOBBY=======================
Object Lobby "The Lobby"
with

	description "The lobby is quiet, all of the workers have gone home for the day leaving you alone in the building. There is a receptionist desk to the north.
		     To the west the lobby extends into a waiting area. To the east the lobby extends towards a restroom. To the south is the elevator you came up on.",

n_to Receptionist,
e_to LobbyEast,
w_to Waiting,
s_to Leave,

before [;
	Go:
		if(selected_direction==s_to) {
			if(computer notin player)
				"You cannot leave without the boss's computer!";
			else deadflag = GS_WIN;
		}
	],

has light;

!================LEAVE============================
!this is a placeholder room for the end of the game

Object Leave "Leave"
with 

	description "test"

has light;

!=============RECEPTIONIST=================
Object Receptionist "Receptionist's Desk"
with

	description "This area is a small cubical where the receptionist works. There is a desk here. To the south there is the lobby.",

s_to Lobby,

has light;

Object -> o7key "loose brass key"
with
	name 'key' 'brass',
	description "This is a single brass key. Stamped on the key are the words ~DUPLICATION STRICTLY PROHIBITED~.";


!=============WAITING======================
Object Waiting "Waiting Room"
with
	description "This is a small waiting area. There is an L-shaped couch with a coffee table in front of it in one corner, and a chair and floor lamp in the other. A window on the west wall looks out on the San Fancisco skyline. To the north there is a long hallway. To the east there is the lobby.",
	cheap_scenery
		'couch' 'l-shaped' "The L-shaped couch is tucked into the corner of this part of the lobby.",

	n_to Hallway7,
	e_to Lobby,

has light;


Object -> chair "heavy-looking chair"
with
    name "chair",
    description "This sturdy and heavy-looking chair matches the L-shaped couch in color.",
has heavy;



Object -> wrlamp "lamp"
with
	name 'lamp' 'torchere' ' torchiere' 'torchiere',
	description "This tall lamp is a classic torchiere of late 20th century design, with a tall, cylindrical post and bowl-like glass diffuser atop a turqoise glass disk. The lamp has a small brass knob for switching it on and off.",
	when_on "The lamp casts a mellow glow on the ceiling.",
	when_off "The lamp is switched off.",
has static switchable;


Object -> coffee_table "coffee table"
with
	name 'coffee table' 'table' 'end table',
	description "This heavy glass table looks like something from a modern art catalog. It is completely asymmetric and has no straight lines or sharp edges.",
has static supporter;

Object -> -> brochure "brochure"
with
	name "brochure" "flyer" "paper",
	description "This full-color tri-fold flyer is printed on sturdy paper. It talks in extravagant terms about the company.";
	

!=============LOBBYEAST===========
Object LobbyEast "Lobby East"
with

	description "This is a small area of the main lobby. To the east there is a door with a sign next to it saying RESTROOM.
		     To the north a hallway extends back into the building. The lobby extends to the west",

n_to Hallway1,
w_to Lobby,
e_to Restroom,

has light;

!=============HALLWAY7(~~~~~~PLACEHOLDER~~~~~)======================
Object Hallway7
with

	description "This is a hallway. To the north the hallway extends. To the south there is the waiting area. To the west there is an office.",

s_to Waiting,
n_to Hallway6,
w_to Office7Door,

has light;

!============OFFICE7(~~~PLACEHOLDER~~~)============================
Object Office7
with

	description "This is an office. To the east there is a door leading back to the hallway.",

e_to Office7Door,

has light;

Object -> Office7Door "Office Door"
with
	name 'office' 'door',
	door_dir (e_to) (w_to),
	found_in Hallway7 Office7,
	with_key o7key,
has static door openable ~open lockable locked;

!=============HALLWAY6(~~~~~~~~~~PLACEHOLDER)=======================
Object Hallway6
with

    description [;
		print "This is a hallway that extends to the north and the south. To the east there is a closet. ";
		if (glass has locked) 
			print "The entire west wall is thick, tempered glass; it stands between you and ";
		else
			print " The west wall used to be glass, but you shattered it. The way is now open to ";

		print "a laboratory packed with futuristic-looking equipment. A biometric authentication system is embedded into the glass wall. It is connected to a large, sliding glass door leading west into the lab.";
],
    cheap_scenery
        'retina' 'scanner' "You peer into the retina scanner. A laser sweeps across your eye. Instantly, the scanner beeps disapprovingly, and indicator on it flashes red several times."
        'voiceprint' 'microphone' "This system appears to be designed to recognize certain individuals' voices when they speak into the microphone."
        'handprint' 'recognizer' [;
			Examine: "The handprint recognizer looks like something from a 1990s spy movie. An angled bezel sticks out from the wall with the outline of a hand illuminated by a pulsating orange grid pattern.";
			Touch: print_ret (string) handfail;
	],

s_to Hallway7,
n_to Hallway5,
e_to ITcloset,
w_to [;
	if (glass has locked)
		"The huge locked glass wall bars the way!";
	else {
		print "You pick your way carefully through the giant shards of glass...^";
		PlayerTo(Office6);
		rtrue;
	}
		
],

has light;

Object -> glass "glass wall"
with
    name 'glass' 'wall' 'door',
    description "A heavy-duty, sliding glass door sits on a track recessed into the floor and ceiling. A piston connected to the biometric tower appears to control the door's movement. The door looks very sturdy.",
    
    door_dir (w_to) (e_to),
    found_in Hallway6 Office6,
	
before [;
	Unlock,Attack:
		if (second has heavy) {
			give self open ~locked;
			"You shatter the glass wall, creating an opening to the next room!";
		} else {
			"You can't break the glass with something that light. Why not find something heavier?";
		}
],

has static door openable ~open lockable locked concealed;
    

Object -> biometric "Biometric Tower"
with
    name 'biometric' 'tower',
    description "This intimidating-looking tower of biometric scanners is securely integrated into to the west wall, which is one large piece of glass. The tower is linked to the actuator that moves the sliding glass door between that separates this hallway and the office to the west. At the top of the tower is a retina scanner with an eyepiece to peer into. Below that is a voiceprint system with a microphone jutting out from it. Below the voiceprint device is a handprint recognizer.",
before [;
    ! FIXME -- see Issue #55 about how this should really work. This feels super hacky.
	Touch: print_ret (string) handfail;
	TalkTo:
		print_ret (string) voxfail;
],
life [;
	Answer: print_ret (string) voxfail;
],
has static animate;


!=============OFFICE6(~~~PLACEHOLDER~~~)===========================
Object Office6
with

	description "This is a laboratory packed with futuristic and delicate-looking equipment. The east wall used to be thick glass, but you shattered it, opening it to the hallway. An expensive biometric authentication system is embedded into the glass wall, along with a mechanism to open and close the door, but you have rendered that completely pointless.",

e_to [;
	if (glass has locked) {
		give glass ~locked;
		print "The motion sensing door opens, letting you into the hallway.";
		PlayerTo(Hallway6);
		print "The door closes behind you.";
		give glass locked;
		rtrue;
	} else {
		print "You carefully step around piles of broken glass and into the hallway.^";
		PlayerTo(Hallway6);
		rtrue;
	}
],

has light;

!==================UTILITY CLOSET===================
Object Utility "The Utility Closet"
with

	description "This is a small utility closet containing cleaning supplies, a mop, a mop bucket, and other things. It's damp and musty in here.",
	cheap_scenery
		'mop' 'mop' "This is one of those old-school mops with a head made from large, grey, cotton, yarn-like strings. It smells faintly of mildew and Pine-Sol. It's hard to imagine how this gross mop could be useful."
		'bucket' 'bucket' "A rolling mop bucket with a built-in mop-squeezer. Embossed on the bucket is a unintentionally comical stick figure of a person slipping on a puddle. It stinks."
		'cleaning' 'supplies' "This small plastic caddy has an assortment of things people would use to clean the office."
		'other' 'things' "Oh, you know, it's just a random assortment of random rags, OSHA signs, and other useless stuff.",

e_to Hallway2,

has light;

Object -> ladder "ladder"
with
	name 'ladder' 'yellow ladder' 'sunflower yellow ladder',
	description "This ladder is a bright sunflower yellow ladder. It must be used to replace the lights if they burn out, without this accessing the ceiling would be impossible.";

!=============HALLWAY5(~~~~~PLACEHOLDER~~~~~)=======================
Object Hallway5
with

	description "This is a hallway, To the east the hallway extends. To the south the hallway extends. To the west there is an office. To the north there is an office.",

s_to Hallway6,
e_to Hallway4,
cant_go "This door is locked. Maybe you could find a key or an alternate way in.",
n_to Office4,

has light;

!==================SPARE OFFICE====================
Object Office4 "The Unused Office"
with

	description "This room is a small office, there is a wooden desk devoid of any identifying obejcts. One of the ceiling panels seems to me missing in here.",

before [;
		Use,Climb:
			if(noun==ladder || 
					(selected_direction==u_to && ladder in player or self)){
				print "You climb up the ladder.";
				if(ladder in player){
					print "  However you had to leave it behind.^";
					<Drop ladder>;
				}
				print "^";
				PlayerTo(Crawl1);
				return true;
			}
],
u_to [;
	if(ladder in player or self){
		<<Use ladder>>;
	}
	"You cannot get up there, if only you had a some sort of step stool.^";
],
s_to Hallway5,

has light;

!===================CRAWL SPACE ENTRANCE=====================
Object Crawl1 "Crawl Space"
with

	description "You climb up into a small crawl space above the ceiling, there are electrical wires running everywhere however it appears as if the wall to the west does not extend up into this space!",

w_to Crawl2,
d_to Office4,

has light;

!================CRAWL SPACE ABOVE BOSS'S OFFICE =====================
Object Crawl2 "Above April's Office Crawl Space"
with

	description "This area seems like it would be directly above April's office.",

d_to Office5,
e_to Crawl1,

has light;

!==================OFFICE5(APRIL'S OFFICE=========================
Object Office5 "April's Office"
with

	description "April's office has a large wooden desk with a computer on it. On the desk there is a name plate that says APRIL. There is a door to the east leading back to the hallway.",

e_to Hallway5,
u_to Crawl2,

has light;

Object -> desk "Desk"
with
	name 'desk' 'wood desk',
	description "This is a desk"

has static supporter;

Object -> -> computer "Computer"
with

	name 'computer',
	description "This is April's computer, this is what you came to take.";

!=============HALLWAY4(~~~~~~~~~PLACEHOLDER~~~~~~)===================
Object Hallway4
with

	description "This is a hallway. To the west the hallway extends. To the east the hallway extends. To the north is an office. To the south is a kitchenette.",

w_to Hallway5,
e_to Hallway3,
n_to Office3,
s_to Kitchenette,

has light;

!============OFFICE3(~~~PLACEHOLDER~~~)==============================
Object Office3
with

	description "This is an office. To the south there is a door leading back to the hallway.",

s_to Hallway4,

has light;

!=============KITCHENETTE===========================================
Object Kitchenette
with

	description "This is a kitchenette. To the north the room extends into the hallway.",

n_to Hallway4,

has light;

Object -> coffeeCard "Coffee Card"
with
	name 'coffee' 'card',
	description "This is a card that can be used to pay for coffee at Moonbux (A national coffee chain).";

!=============HALLWAY3(~~~~~~~~~PLACEHOLDER~~~~~~)===================
Object Hallway3
with

	description "This is a hallway. To the south the hallway extends. To the west the hallway extends. To the east is an office.",

w_to Hallway4,
s_to Hallway2,
e_to Office2,

has light;

Object -> plant "large potted plant"
with
	name "plant" "potted plant" "pot",
	description "This is a large plant in a large glazed terra cotta pot. It looks like it must weigh a lot!",
has heavy;

!============OFFICE2(~~~PLACEHOLDER~~~)==============================
Object Office2
with

	description "This is the IT office. The floor of this room is made of large square modular linoleum-covered panels of the kind typical in server rooms with raised floors. A desk is in the corner has an empty laptop docking station on it and a few fidget toys. A work table is against the south wall with the electronic flotsam and jetsam of a previous project. The useless husks of a few broken computers lie in the corner. To the west there is a door leading to the hallway. To the north is a reinforced door with an electronic keypad lock.",
	cheap_scenery
		'lock' 'keypad' "The keypad presumably controls the door lock; entering the right code would likely unlock the door."
		'husks' 'computers' "These computers are definitely useless junk, and they smell like the magic smoke got out of them."
		'work' 'table' "The work table has bits and pieces of wire, some discrete electronic components -- nothing much interesting."
		'reinforced' 'door' "This door looks extremely strong, as does the wall it is set into."
		'fidget' 'toys' "One toy is your typical fidget spinner, and the other is a folding cube toy."
		'docking' 'station' "This generic docking station is connected to a monitor and keyboard, but the laptop is not here. A wired Ethernet cable that is plugged into the back of of the dock snakes across the desk and down through a hole in the floor tile.",

w_to Hallway3,
d_to ITRoomTile,

has light;

Object -> ITRoomTile "Floor Tile"
with 
! FIXME: This currently requires the player to unlock the tile with the plunger
! and open the floor with the plunger. This, of course, is bananas.
! See Issue #38:
! https://github.com/UMDLARS/one_night_in_sf/issues/38
! There's just one problem -- I have no idea how to implement that!
	name "floor" "tile" "floor tile",
	door_dir (d_to) (u_to),
	found_in Office2 UnderITRoom,
	with_key plunger,
has static door openable ~open lockable locked;

!=============UnderItRoom(~~~~~~PLAACEHOLDER~~~~~~~)====================
Object UnderItRoom
with
	description "You are underneath a raised floor. This cramped and dusty space is about one foot tall. Spaced every two feet are steel pillars; resting on top of the pillars are precisely fitted square floor tiles, held only by gravity. One tile has been removed. Ethernet wires run through chases alongsize electrical condiut and air conditioning ducts. The part of the crawlspace you are in seems to be about the same size as the IT office, but the space extends far enough to the north that it disappears in darkness.",

u_to Office2,
n_to UnderServerRoom;

Object Note
with
	description "This is a note";

!=============UnderServerRoom(~~~~~~PLAACEHOLDER~~~~~~~)====================
Object UnderServerRoom
with
	description "You are underneath a raised floor. Dust bunnies are everywhere. Spaced every two feet are steel pillars; resting on top of the pillars are precisely fitted square floor tiles, held only by gravity. Light streams through holes in the tiles above your head; Ethernet cables snake through those holes into the light above. The muffled roar of server racks can be heard above your head. In the darkness to the south, a large square of light is visible.",

u_to ServerRoom,
s_to UnderITRoom;


!=============ServerRoom(~~~~~~PLAACEHOLDER~~~~~~~)====================
Object ServerRoom
with
	description "This is a medium-sized server room with several racks of servers in a line down the middle of the room. The floor in this room is made of large square modular linoleum-covered panels; one of the panels has been lifted out of its place, leaving a large square hole in the floor, revealing a crawlspace. One of the racks contains a large tape backup system. Their blinking lights and roaring fans are quite impressive. Sturdy locked cages protect the hardware from unauthorized tampering. A table and chair sit in the corner of the room. A reinforced door leads south into the IT office; there is no lock on this side.",
	cheap_scenery
		'server' 'racks' "Four server racks with locked cages stand in a row; inside them are stacks of identical servers. One of the racks has a tape backup server in it."
		'chair' 'chair' "This is an old, dumpy-looking rolling office chair.",

d_to UnderServerRoom,
s_to Office2,

has light;

Object -> server_tile "floor tile"
with
	name 'tile' 'floor tile',
	description "This roughly two-foot by two-foot tile lies crooked on the floor, having been removed from its perfectly square-shaped spot in the floor. It is made out of heavy-duty particle board with a metal perimeter topped with white linoleum flecked with gray. It looks heavy.",

has static supporter;


Object -> server_table "table"
with
	name 'table',
	description "This generic work table has a locked computer workstation on it.",

has static supporter;

Object -> -> backup "backup tape"
with
	name "backup" "tape" "backup tape",
	description "This is a plastic one-terabyte backup tape cartridge. On the cartridge is a handwritten sticky note that says ~Full Backup (UNENCRYPTED)~ and yesterday's date.";

!=============HALLWAY2(~~~~~~PLAACEHOLDER~~~~~~~)====================
Object Hallway2
with

	description "This is a hallway. To the south the hallway extends. To the north the hallway extends. To the west there is a utility closet.",

n_to Hallway3,
s_to Hallway1,
w_to Utility,

has light;

!=============ITCLOSET=============================================
Object ITcloset
with

	description "This is an IT closet. To the east there is a door leading back to the hallway.",

w_to Hallway6,

has light;

Object -> screwdriver "Screwdriver"
with
	name 'screwdriver',
	description "This is a screwdriver.";

Object -> flashlight "flashlight"
with
	name 'flashlight' 'torch' 'flash' 'light' 'lamp',
	description [;
		print "This cheap flashlight has a Tastytronic Industries logo emblazoned on it.  It is currently ";
		if (self has on) "on.";
		"off.";
	],

	when_on "There is a flashlight here (turned on).",
	when_off "There is a flashlight here (turned off).",
	after [;
		SwitchOn: give self light;
		SwitchOff: give self ~light;
	],
has switchable;

!=============HALLWAY1(~~~~~~PLACEHOLDER~~~~~~~)====================
Object Hallway1
with

	description "To the south there is the lobby. To the north the hallway extends. To the east there is an office.",

n_to Hallway2,
s_to LobbyEast,
e_to Office1Door,

Before[;
	Jimmy,Unlock:
		if(noun==Office1Door && second==coffeeCard){
			give Office1Door ~locked;
			give Office1Door open;
			print "You unlock the office door and open it!^";
			return true;
		}
],

has light;

!=============OFFICE1(ELLIE'S OFFICE)============================
Object Office1 "Ellie's Office"
with

	description [;
			if(ellieComputer has locked)
"In this office there is a wood desk with a plaque with the name ~Ellie~ on it, a monitor, a keyboard, and a mouse on it, and a computer case next to it. The computer appears bolted down to the floor. On the back there are 4 screws enclosing the case. To the west there is a door heading back to the hallway.";
			else
"In this office there is a wood desk with a plaque with the name ~Ellie~ on it, a monitor, a keyboard, and a mouse on it, and a computer case next to it. The computer appears to bolted down to the floor. On the back there were 4 screws enclosing the case, however you have removed them, allowing access to the inside of the computer! To the west there is a door leading back to the hallway.";
],
cheap_scenery
	CS_NO_ADJ 'screws' [;
		Examine: "These are 4 phillips head screws, maybe you could unscrew them with something.";
		Turn: if(second==screwdriver){
				print "You unscrew the back panel of the computer exposing the inside of it!^";
				give ellieComputer ~locked;
				give ellieComputer open;
				return true;
			};
		default: "I am not sure what you want me to do.";

],
w_to Office1Door,
Before[;
	Use:
		if(noun==ellieComputer){
			print "You attempt to use the computer, however, you are immediately challenged by a log in screen requesting a password. It seems the information on the hard drive of this
			       computer will have to be accessed some other way.^";
			return true;
		}

		if(noun==screwdriver && second==ellieComputer){
			print "You unscrew the back panel of the computer exposing the inside of it!^";
			give ellieComputer ~locked;
			give ellieComputer open;
			return true;
		}
	Unlock:
		if(noun==ellieComputer && second==screwdriver){
			print "You unscrew the 4 screws at the back of the computer opening the case!^";
			give ellieComputer ~locked;
			give ellieComputer open;
			return true;
		}

],

has light;

Object -> ellieDesk "Desk"
with
	name 'desk' 'wood',
	description "This is a large wood desk. On its top there is a small plaque with the name ELLIE, a keyboard, a mouse, and a monitor connected to the computer next to this desk."

has static supporter;

Object -> ellieComputer "Computer"
with
	name 'computer' 'case',
	description "This computer is bolted down to the floor as to prevent it from being moved. On the back there are a number of wires exiting the computer. On the side there are 4 screws holding the
		     side panel on.",
	with_key screwdriver,

has static openable lockable locked container;

Object -> -> hardDrive "Ellie's Hard Drive"
with
	name 'HDD' 'hard' 'drive',
	description "This is a Hard Disk Drive from Ellie's computer, if you take this, you're sure you can get the information on it at a later time.";

Object -> Office1Door "Office Door"
with
	name 'office' 'door',
	description "This is an old wood antique door. You can see it has an incredibly simple locking mechanism, it could easy unlocked if you had a thin stiff object to jimmy the lock.",
	door_dir (e_to) (w_to),
	found_in Hallway1 Office1,
	with_key coffeeCard,
has static door openable ~open lockable locked;


!=============RESTROOM=====================
Object Restroom "Restroom"
with

	description "This room is a small single user bathroom. There is a sink with a mirror as well as a toilet. To the west there is a door leading back into the lobby.",

w_to LobbyEast,
has light;

Object -> plunger "toilet plunger"
with
	name 'plunger' 'toilet plunger',
	description "This is your bog-standard toilet plunger: a wooden handle screwed into a large and flexible rust-colored rubber suction cup.";

